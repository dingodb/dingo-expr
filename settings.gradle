/*
 * Copyright 2021 DataCanvas
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rootProject.name = 'dingo-expr'

// function String.v()
String.metaClass.v = { ext[(delegate as String) + '.version'] }

gradle.ext.gitPropertiesKeys = [
    'git.branch',
    'git.commit.id',
    'git.commit.time',
    'git.dirty',
    'git.remote.origin.url',
    'git.tags',
    'git.total.commit.count',
]

gradle.ext.setOssrhRepo = { handler, project ->
    handler.maven {
        name = "OSSRH"
        def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        url = (project.version as String).endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
            username = System.getenv("MAVEN_USERNAME") != null
                ? System.getenv("MAVEN_USERNAME")
                : project.findProperty("ossrhUsername")
            password = System.getenv("MAVEN_TOKEN") != null
                ? System.getenv("MAVEN_TOKEN")
                : project.findProperty("ossrhPassword")
        }
    }
}

gradle.ext.setPom = {
    it.pom {
        url = 'https://www.dingodb.com/'
        scm {
            connection = 'scm:git:git@github.com:dingodb/dingo-expr.git'
            developerConnection = 'scm:git:git@github.com:dingodb/dingo-expr.git'
            url = 'https://github.com/dingodb/dingo-expr/tree/main'
        }
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'dingodb'
                name = 'DingoDB Develop Team'
                email = 'dingodb@zetyun.com'
            }
        }
    }
}

// Modules
include 'annotations'
findProject(':annotations')?.name = 'dingo-expr-annotations'
include 'coding'
findProject(':coding')?.name = 'dingo-expr-coding'
include 'console'
findProject(':console')?.name = 'dingo-expr-console'
include 'jni'
findProject(':jni')?.name = 'dingo-expr-jni'
include 'json'
findProject(':json')?.name = 'dingo-expr-json'
include 'parser'
findProject(':parser')?.name = 'dingo-expr-parser'
include 'runtime'
findProject(':runtime')?.name = 'dingo-expr-runtime'
include 'test'
findProject(':test')?.name = 'dingo-expr-test'
