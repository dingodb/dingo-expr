/*
 * Copyright 2021 DataCanvas
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-conventions'
    id 'java-library'
    id 'com.gorylenko.gradle-git-properties' version "${gradleGitPropertiesVersion}"
    id "com.vanniktech.maven.publish"
}

dependencies {
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: 'mapstruct'.v()
    api project(':dingo-expr-parser')
    api project(':dingo-expr-runtime')
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: 'jackson'.v()
    implementation group: 'com.google.guava', name: 'guava', version: 'guava'.v()
    implementation group: 'commons-io', name: 'commons-io', version: 'commons-io'.v()
    implementation group: 'org.mapstruct', name: 'mapstruct', version: 'mapstruct'.v()
    implementation project(':dingo-expr-json')
    implementation project(':dingo-expr-common')
    testImplementation group: 'com.google.guava', name: 'guava', version: 'guava'.v()
    testImplementation project(':dingo-expr-json')
    testImplementation project(':dingo-expr-test')
    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: 'logback'.v()
    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-core', version: 'logback'.v()
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.whenTaskAdded { task ->
    if (task.name.contains('JavadocJar') || task.name.contains('SourcesJar')) {
        task.enabled = false
    }
}

afterEvaluate {
    tasks.named("generateMetadataFileForMavenPublication") {
        dependsOn tasks.named("plainJavadocJar")
    }
}

mavenPublishing {
    coordinates(project.property("GROUP") as String, "dingo-expr-rel", project.property("VERSION") as String)
    
    pom {
        name = "dingo-expr-rel" 
        description = "Relational expression library of Dingo Expression."
        url= "https://www.dingodb.com/"
        
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        
        developers {
            developer {
                id = "dingodb"
                name = "DingoDB develop team"
                email = "dingodb@zetyun.com"
            }
        }
        
        scm {
            connection = "scm:git:git://github.com/dingodb/dingo-expr.git"
            developerConnection = "scm:git:ssh://github.com/dingodb/dingo-expr.git"
            url = "https://github.com/dingodb/dingo-expr"
        }
    }
    
    publishToMavenCentral()
    // Conditionally enable signing only for Central tasks
    if (gradle.startParameter.taskNames.any { it.contains('MavenCentral') || it.contains('publishToMavenCentral') }) {
        signAllPublications()
    }
}
