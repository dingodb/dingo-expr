/*
 * Copyright 2021 DataCanvas
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'checkstyle'
    id 'signing'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.named('jar') {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Build-Timestamp": new Date().format("yyyy-MM-dd HH:mm:ss.SSSZ"),
            "Build-Jdk": "${System.properties['java.version']} "
                + "(${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            "Build-OS": "${System.properties['os.name']}"
                + " ${System.properties['os.arch']} ${System.properties['os.version']}",
        )
    }
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // facilities
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: 'lombok'.v()
    compileOnly group: 'org.projectlombok', name: 'lombok', version: 'lombok'.v()
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: 'lombok'.v()
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: 'lombok'.v()
    // testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'junit5'.v()
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: 'junit5'.v()
    testImplementation group: 'org.assertj', name: 'assertj-core', version: 'assertj'.v()
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'junit5'.v()
    // logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: 'slf4j'.v()
    // static check
    checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: 'checkstyle'.v()
    compileOnly group: 'org.checkerframework', name: 'checker-qual', version: 'checkerframework'.v()
    testCompileOnly group: 'org.checkerframework', name: 'checker-qual', version: 'checkerframework'.v()
}

tasks.named('test') {
    useJUnitPlatform()
}
